#!/usr/bin/env fish
#
# Takes as input a bunch of .zip files from my "Adventures in Odyssey" Google
# Drive folder. Sorts and flattens their contents, and intersperses periods of
# silence. The result is a set of files suitable for copying to a cheap MP3
# player.
#
# How to use:
#
#   1) rm -rf ~/Downloads/*
#
#   2) Open
#      https://drive.google.com/drive/u/1/folders/11_Sc9Mn2v7GaaoBFMbC13OEq7RDw8R4y
#      in a browser.
#
#   3) Right-click on "Albums" in the top breadcrumb bar. Click "Download".
#      Wait for all the downloads to complete.
#
#   4) cd ~/Downloads && odyssey-mp3-playlist *.zip
#
#   5) "~/Downloads/Playlists" now contains a flat set of files which can be
#      copied to the MP3 player.

# Extract the .zip files. This will produce files under ~/Downloads/Albums.
for zip in $argv
  unzip $zip
  or exit
end

set albumsDir ./Albums

# Make an output directory.
set playlistsDir ./Playlists
mkdir -p $playlistsDir
or exit

# Fetch a silence track.
set silenceFile ./silence.mp3
curl \
  https://raw.githubusercontent.com/sethpollen/sbp_linux_config/master/cookbooks/26-hours-of-silence.mp3 \
  --output $silenceFile
or exit

set counter 0

set albums (ls $albumsDir)
echo '##' (count $albums) albums found

for album in $albums
  set episodes (find "$albumsDir/$album" -name '*.mp3' -printf '%f\n' | sort)
  echo '##' $album has (count $episodes) episodes

  for episode in $episodes
    if test (math $counter % 3) -eq 0
      # Insert silence after each group of 2 episodes.
      echo Inserting silence
      cp $silenceFile \
          (printf '%s/%03d_silence.mp3' $playlistsDir $counter)
    else
      # Copy the next episode over.
      echo Copying "$albumsDir/$album/$episode"
      cp "$albumsDir/$album/$episode" \
          (printf '%s/%03d_%s_%s' $playlistsDir $counter "$album" "$episode")
    end

    set counter (math $counter + 1)
  end

  # Make sure each album starts with silence.
  set counter (math $counter + 3 - $counter % 3)
end

# Clean up.
rm $silenceFile
rm -rf $albumsDir
